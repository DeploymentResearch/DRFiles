<job id="Install-MicrosoftSilverlight5x64">
<script language="VBScript" src="..\..\scripts\ZTIUtility.vbs"/>
<script language="VBScript"> 

'//----------------------------------------------------------------------------
'// Purpose: Used to install Microsoft Silverlight 5.0 x64
'// Usage: cscript Install-MicrosoftSilverlight5x64.wsf [/debug:true]
'// Version: 1.2 - August 8, 2012 - Johan Arwidmark
'//
'// This script is provided "AS IS" with no warranties, confers no rights and 
'// is not supported by the authors or Deployment Artist. 
'//
'//----------------------------------------------------------------------------

'//----------------------------------------------------------------------------
'// Global constant and variable declarations
'//---------------------------------------------------------------------------- 

Option Explicit 
Dim iRetVal 

'//----------------------------------------------------------------------------
'// End declarations
'//---------------------------------------------------------------------------- 

'//----------------------------------------------------------------------------
'// Main routine
'//---------------------------------------------------------------------------- 

On Error Resume Next
iRetVal = ZTIProcess
ProcessResults iRetVal
On Error Goto 0 

'//---------------------------------------------------------------------------
'//
'// Function: ZTIProcess()
'//
'// Input: None
'// 
'// Return: Success - 0
'// Failure - non-zero
'//
'// Purpose: Perform main ZTI processing
'// 
'//---------------------------------------------------------------------------

Function ZTIProcess() 

	Dim sSetupFile
	Dim sArguments

	sSetupFile = oUtility.ScriptDir & "\Source\Silverlight_x64.exe"
	sArguments = "/q"

	oLogging.CreateEntry oUtility.ScriptName & ": Starting installation", LogTypeInfo
	
	If not oFSO.FileExists(sSetupFile) then
		oLogging.CreateEntry oUtility.ScriptName & ": " & sSetupFile & " was not found, unable to install", LogTypeError
		ZTIProcess = Failure
		Exit Function
	End if

	iRetVal = oUtility.RunWithHeartbeat("""" & sSetupFile & """ " & sArguments)
	
	if (iRetVal = 0) or (iRetVal = 3010) then
		ZTIProcess = Success 
	Else 
		ZTIProcess = Failure
	End If
	
	oLogging.CreateEntry oUtility.ScriptName & ": Return code from command = " & iRetVal, LogTypeInfo
	oLogging.CreateEntry oUtility.ScriptName & ": Finished installation", LogTypeInfo
	
End Function 

</script>
</job>
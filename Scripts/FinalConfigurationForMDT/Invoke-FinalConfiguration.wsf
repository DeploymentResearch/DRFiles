<job id="Invoke-FinalConfiguration">
   <script language="VBScript" src="..\..\scripts\ZTIUtility.vbs"/>
   <script language="VBScript">
 
'//----------------------------------------------------------------------------
'// Solution: Hydration
'// Purpose: Used to do some custom configuration after task sequence completes
'// Usage: cscript Invoke-FinalConfiguration.wsf [/debug:true]
'// Version: 1.6 - October 25, 2022 - Johan Arwidmark
'//
'// This script is provided "AS IS" with no warranties, confers no rights and 
'// is not supported by the authors or Deployment Artist. 
'//
'//----------------------------------------------------------------------------

'//
'// Global constant and variable declarations
'//
'//---------------------------------------------------------------------------- 

Option Explicit 
Dim iRetVal 

'//----------------------------------------------------------------------------
'// End declarations
'//---------------------------------------------------------------------------- 

'//----------------------------------------------------------------------------
'// Main routine
'//---------------------------------------------------------------------------- 

'On Error Resume Next
iRetVal = ZTIProcess
ProcessResults iRetVal
On Error Goto 0 

'//---------------------------------------------------------------------------
'//
'// Function: ZTIProcess()
'//
'// Input: None
'// 
'// Return: Success - 0
'// Failure - non-zero
'//
'// Purpose: Perform main ZTI processing
'// 
'//---------------------------------------------------------------------------

Function ZTIProcess() 
     	iRetVal = Success 
     	ZTIProcess = iRetval 

	Dim sSName,sCMD,sReturn,iZTIRetValue,sDomain,sFile,sTemporaryFolder,f

	sSName=oUtility.ScriptName
	'set the return value to assume a failure
	iZTIRetValue="1"

	'Start logging for this script
	oLogging.CreateEntry sSName & ": Starting  actions*********************************************************",LogTypeInfo

	' Check for SkipFinalSummary value being set to YES
	If UCASE(oEnvironment.Item("SkipFinalSummary")) = "YES" Then
	   oLogging.CreateEntry sSName & "SkipFinalSummary set to YES, continuing",LogTypeInfo
	Else
	   oLogging.CreateEntry sSName & "SkipFinalSummary not set to YES, aborting",LogTypeError
	   ZTIProcess = Failure
	   Exit Function
	End if

	' Check for FinishAction not being configured
	If UCASE(oEnvironment.Item("FinishAction")) <> "" Then
	   oLogging.CreateEntry sSName & "FinishAction is not set, continuing",LogTypeInfo
	Else
	   oLogging.CreateEntry sSName & "FinishAction is configured to: " & oEnvironment.Item("FinishAction") & ", aborting",LogTypeError
	   ZTIProcess = Failure
	   Exit Function
	End if

	
	'Copying script to local machine, temp folder
	Const TemporaryFolder = 2
	Set sTemporaryFolder = oFSO.GetSpecialFolder(TemporaryFolder)
	sFile = oUtility.ScriptDir & "\FinalConfiguration.hta"
    oFSO.GetFile(sFile).Copy sTemporaryFolder & "\" & oFSO.GetFileName(sFile),True
	sFile = oUtility.ScriptDir & "\Servers.png"
    oFSO.GetFile(sFile).Copy sTemporaryFolder & "\" & oFSO.GetFileName(sFile),True
	sFile = oEnvironment.Item("DEPLOYROOT") & "\Scripts\Wizard.ico"
    oFSO.GetFile(sFile).Copy sTemporaryFolder & "\" & oFSO.GetFileName(sFile),True
	sFile = oEnvironment.Item("DEPLOYROOT") & "\Scripts\Wizard.css"
    oFSO.GetFile(sFile).Copy sTemporaryFolder & "\" & oFSO.GetFileName(sFile),True

	'Create the cmd to start the HTA
	sDomain = oEnvironment.Item("JOINDOMAIN")
	sCmd="mshta.exe """ & sTemporaryFolder & "\FinalConfiguration.hta"" " & Chr(34) & sDomain & Chr(34) 

	'Log the cmd we are going to run
	oLogging.CreateEntry "About to run " & sCmd,LogTypeInfo

	'start the HTA, but don't wait for it to complete - otherwise we'll never end the TS.
	oShell.CurrentDirectory = sTemporaryFolder
	sReturn=oShell.run (sCmd,1,False)

	'Log the return code from starting the HTA
	oLogging.CreateEntry "Return code from " & sCMD & " was : " & sReturn,LogTypeInfo

	'End Logging for this script
	oLogging.CreateEntry sSName & ": Completed actions *********************************************************",LogTypeInfo

	If sReturn="0" Then
		iZTIRetValue = Success
		oLogging.CreateEntry "HTA was started succesfully",LogTypeInfo
	Else
		iZTIRetValue = Failure
		oLogging.CreateEntry "HTA failed to start",LogTypeError
	End If

	ZTIProcess=iZTIRetvalue
End Function 

</script>
</job>
